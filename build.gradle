plugins {
	id 'java'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.2'
}

group 'com.moovel'
version = '1.0.0-1'

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext.rundeckPluginVersion = '1.2'

// Set this to a comma-separated list of full classnames of your implemented Rundeck plugins.
ext.pluginClassNames = 'org.rundeck.plugins.S3LogFileStoragePlugin'


repositories {
	mavenLocal()
	mavenCentral()
}

configurations {
	//declare custom pluginLibs configuration to include only libs for this plugin
	pluginLibs

	//declare compile to extend from pluginLibs so it inherits the dependencies
	compile.extendsFrom pluginLibs
}

dependencies {
	// add any third-party jar dependencies you wish to include in the plugin
	// using the `pluginLibs` configuration as shown here:
	pluginLibs 'com.amazonaws:aws-java-sdk-s3:1.10.30'

	//the compile dependency won't add the rundeck-core jar to the plugin contents
	compile 'org.rundeck:rundeck-core:2.6.1'

	// dependencies required for testing only
	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-core:1.10.19'
}

jar {
	from(configurations.pluginLibs) {
		into("lib")
	}
	manifest {
		def libList = configurations.pluginLibs.collect { 'lib/' + it.name }.join(' ')
		attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
		attributes 'Rundeck-Plugin-Archive': 'true'
		attributes 'Rundeck-Plugin-Classnames': pluginClassNames
		attributes 'Rundeck-Plugin-File-Version': version
		attributes 'Rundeck-Plugin-Libs': "${libList}"
		attributes 'Main-Class': "org.rundeck.plugins.S3LogFileStoragePlugin"
		attributes 'Class-Path': "${libList}"
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives sourcesJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact(sourcesJar)
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
	publications = ['mavenJava']
	pkg {
		repo = 'maven'
		name = project.name
		userOrg = 'moovel'
	}
}
